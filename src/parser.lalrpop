use prelude::*;

use algorithm;
use data;
use item;
use table;

grammar;

//
//  Items
//

pub Type: item::ObjectType = {
    <Item*> => item::link(<>),
};

Item: (String, item::Item) = {
    TableSignature => (<>.0, item::Item::TableSignature(<>.1)),
    TableTerm => (<>.0, item::Item::TableTerm(<>.1)),
    TableInstance => {
        let instance_name = <>.0;
        let signature = <>.1;
        let implementors = <>.2.into_iter().collect();
        (instance_name, item::Item::TableInstance { signature, implementors })
    },
};

TableTerm: (String, table::TableTerm) = {
    Initializer => (<>.0, table::TableTerm::Constructor(<>.1)),
    Action => (<>.0, table::TableTerm::Action(<>.1)),
};

//
//  Tables
//

TableSignature: (String, Dict<table::SignatureTerm>) = {
    <name: Ident> ":=" "role" "{" <terms: Comma<TableSignatureTerm>> "}" =>
        (name, terms.into_iter().collect()),
};

TableSignatureTerm: (String, table::SignatureTerm) = {
    <Ident> ":" <TermSignature>,
};

TermSignature: table::SignatureTerm = {
    "action" "(" <Comma<DataType>> ")"
        => table::SignatureTerm::Action(<>),
    "initializer" "(" <Comma<DataType>> ")"
        => table::SignatureTerm::Initializer(<>),
};

DataType: data::FieldType = {
    "Num" => data::FieldType::Num,
};

TableInstance: (String, String, Vec<(String, String)>) = {
    <Ident> ":=" <Ident> "{" <Comma<Assignment<Ident>>> "}",
};


//
//  Algorithms
//

Initializer: (String, algorithm::Algorithm) = {
    <Ident> ":=" "initializer" <Algorithm>,
};

Action: (String, algorithm::Algorithm) = {
    <Ident> ":=" "action" <Algorithm>,
};

Algorithm: algorithm::Algorithm = {
    "(" <param_list: Comma<Ident>> ")" "{" <steps: Blocks> "}" => {
        algorithm::Algorithm { <> }
    },
};

Blocks: Vec<algorithm::Statement> = {
    <blocks: Block*> => blocks.into_iter().flat_map(|x| x).collect()
};

Block: Vec<algorithm::Statement> = {
    <Statement> => vec![<>],
    "for" <var_name: Ident> "in" <set_name: Ident> "{" <block: Blocks> "}" => {
        let mut block = block;
        let break_offset = block.len() + 1;
        let intro = algorithm::Statement::SetIterate {
            var_name,
            set_name,
            break_offset,
        };
        block.insert(0, intro);

        block.push(algorithm::Statement::Continue);

        block
    },
};

Statement: algorithm::Statement = {
    "BANG;" => algorithm::Statement::Debug("BANG".into()),
    "print" "(" <Comma<Expression>> ")" ";" => {
        algorithm::Statement::DebugNums(<>)
    },
    "cancel;" => algorithm::Statement::CancelWait,
    <results: Comma<Ident>> "=" <expressions: Comma<Expression>> ";" => {
        algorithm::Statement::Assign{ <> }
    },
    "self" "=" <name: Ident> "{" <terms: Dict<Ident>> "}" ";" => {
        algorithm::Statement::State { <> }
    },
    "wait" "(" <Expression> ")" ";" => {
        algorithm::Statement::Wait(<>)
    },
    <set_name: Ident> "." "add" "(" <to_add: Expression> ")" ";" => {
        algorithm::Statement::SetAdd { <> }
    },
    <set_name: Ident> "." "remove" "(" <to_remove: Expression> ")" ";" => {
        algorithm::Statement::SetRemove { <> }
    },
    <typ: Ident> "." <table: Ident> "." <init_name: Ident>
        "(" <args: Comma<Expression>> ")" ";"
        => algorithm::Statement::GotoAlg {
            table: algorithm::TablePath::Static(typ, table),
            alg_name: init_name,
            args,
        },
    <object_name: Ident> "." <action_name: Ident>
        "(" <args: Comma<Expression>> ")" ";"
        => algorithm::Statement::GotoAlg {
            table: algorithm::TablePath::Virtual(object_name),
            alg_name: action_name,
            args,
        },
};

//
//  Expressions
//

Expression: algorithm::Expression = {
    SumExpression,
};

SimpleExpression: algorithm::Expression = {
    "(" <Expression> ")",
    <Ident> => algorithm::Expression::MoveVar(<>),
    "copy" "(" <Ident> ")" => algorithm::Expression::CloneVar(<>),
    <type_name: Ident> "." <table_name: Ident>
        "." <init_name: Ident> "(" <args: Comma<Expression>> ")"
        => algorithm::Expression::InitObject { <> },
    <object_name: Ident> "."
        <action_name: Ident> "(" <args: Comma<Expression>> ")"
        => algorithm::Expression::ExecObject { <> },
    "Set" "." "new" "(" ")" => algorithm::Expression::InitSet,
    "game" "." <function_name: Ident> "(" <args: Comma<Expression>> ")"
        => algorithm::Expression::ExternCall { <> },
    Number => algorithm::Expression::Const(<>),
    "self" => algorithm::Expression::SelfObject,
    <interface_name: Ident> "(" <object: Expression> ")" => {
        let object = Box::new(object);
        algorithm::Expression::VirtualizeObject { interface_name, object }
    },
};

SumExpression: algorithm::Expression = {
    MulExpression,
    <x: SumExpression> "+" <y: MulExpression> =>
        algorithm::Expression::Add(Box::new(x), Box::new(y)),
    <x: SumExpression> "-" <y: MulExpression> =>
        algorithm::Expression::Sub(Box::new(x), Box::new(y)),
};
MulExpression: algorithm::Expression = {
    PowExpression,
    <x: MulExpression> "*" <y: PowExpression> =>
        algorithm::Expression::Mul(Box::new(x), Box::new(y)),
    <x: MulExpression> "/" <y: PowExpression> =>
        algorithm::Expression::Div(Box::new(x), Box::new(y)),
};
PowExpression: algorithm::Expression = {
    <SimpleExpression>,
    <x: PowExpression> "^" <y: SimpleExpression> =>
        algorithm::Expression::Pow(Box::new(x), Box::new(y)),
};

Number: f64 = {
    r"[0-9]+" => <>.parse().unwrap(),
    r"[0-9]*\.[0-9]+" => <>.parse().unwrap(),
};

//
//  General Purpose Things
//

Dict<T>: Dict<T> = {
    <Comma<Assignment<T>>> => <>.into_iter().collect()
};

Assignment<T>: (String, T) = {
    <Ident> "=" <T>
};

Ident: String = {
    r"[a-zA-Z_][a-zA-Z0-9_]*" => <>.into(),
};

// taken directly from
// http://lalrpop.github.io/lalrpop/tutorial/007_macros.html
Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};
